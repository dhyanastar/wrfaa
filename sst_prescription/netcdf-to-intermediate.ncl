; netcdf-to-intermediate.ncl
; Jeonghoe Kim <jeonghoekim.14@snu.ac.kr>
;
; This NCL script generates WPS intermediate files from processed NetCDF files,
; by using the outputs generated by "interp_sst.py".

begin

; SST dataset.
dataset = "OISST"
; Available options:
;   1. OSTIA
;   2. OSTIAdiu
;   3. OISST
;   4. GHRSST_JPL_4.1
;   5. GHRSST_JPL_4.2
;   6. GHRSST_NCEI
;   7. GHRSST_UKMO

; Location of the input files (interpolated NetCDF files).
loc_in     = "./interp"
; Location of the output files (WRF intermediate files).
loc_out    = "./interm"

;----------------------------------------------------------------------;

fils = systemfunc("ls " + loc_in + "/" + dataset + "_*interp.nc | grep .nc")

do i = 0, dimsizes(fils)-1
  ; print("Processing " + fils(i) + " ..." + "")
  ncdf = addfile(fils(i), "r")

  time := ncdf->time
  lat = ncdf->lat
  lon = ncdf->lon
  sst = ncdf->sst

  time := cd_calendar(time, -3)
  time := tostring(time)    ; YYYYMMDDHH

  ; YYYYMMDDHH -> YYYYMMDD_HH
  dummy = str_split_by_length(time, 2)
  time := dummy(0) + dummy(1) + "-" + dummy(2) + "-" + dummy(3) + "_" + dummy(4)

  opt                        = True
  opt@projection             = "Equidistant_Lat_Lon"
  opt@date                   = time
  opt@level                  = 200100
  opt@startloc               = "SWCORNER"
  opt@startlat               = lat(0)
  opt@startlon               = lon(0)
  opt@deltalat               = lat(1)-lat(0)
  opt@deltalon               = lon(1)-lon(0)
  opt@is_wind_earth_relative = False

  wrf_wps_write_int("FILE:" + dataset, "SST", sst@units, "Sea-Surface Temperature", sst, opt)

  ; Attach masking information to the intermediate file to provide higher resolution of land/sea mask.
  ; Refer to <https://forum.mmm.ucar.edu/threads/resolved-landsea-mask-best-practices.9032/>.
  if (.not. use_interp) then
    msk = ncdf->msk
    wrf_wps_write_int("FILE:" + dataset, "SST_MASK", msk@units, "Land Sea Mask", msk, opt)
  end if 

end do

system("mv " + "FILE:" + dataset + ":* " + loc_out)

end
